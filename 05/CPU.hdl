// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // load to ARegister
    Mux16(a=instruction, b=aluout, sel=instruction[15], out=a0);
    Not(in=instruction[15], out=insa);
    Or(a=insa, b=instruction[5], out=loada); 
    ARegister(in=a0, load=loada, out=aout, out[0..14]=addressM);
    
    // load to DRegister
    And(a=instruction[4], b=instruction[15], out=loadd);
    DRegister(in=aluout, load=loadd, out=dout);
    
    // input to ALU
    Mux16(a=aout, b=inM, sel=instruction[12], out=mout);
    ALU(x=dout, y=mout,
      zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6],
      out=aluout, out=outM, zr=zr, ng=ng);
    
    // write to writeM flag
    And(a=instruction[3], b=instruction[15], out=writeM);
    
    // JMP logic
    And(a=zr, b=instruction[1], out=j1);
    And(a=ng, b=instruction[2], out=j2);
    Or(a=ng, b=zr, out=ngzr);
    Not(in=ngzr, out=pos);
    And(a=pos, b=instruction[0], out=j3);
    
    Or(a=j1, b=j2, out=l1);
    Or(a=j3, b=l1, out=l2);
    And(a=l2, b=instruction[15], out=jump);
    Not(in=jump, out=isinc);

    PC(in=aout, load=jump, inc=isinc, reset=reset, out[0..14]=pc);
}